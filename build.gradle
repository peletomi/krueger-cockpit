buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-3595"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("""A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.""".stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = "http://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.0-beta-3595"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.0.0-beta-3595"
    compile "org.apache.logging.log4j:log4j:2.4.1"
    compile "org.apache.logging.log4j:log4j-api:2.4.1"
    compile "org.apache.logging.log4j:log4j-core:2.4.1"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.4.1"
    compile "io.vertx:vertx-core:3.1.0"
    compile "io.vertx:vertx-web:3.1.0"
    compile "io.vertx:vertx-dropwizard-metrics:3.1.0"
    compile "com.netflix.hystrix:hystrix-core:1.4.20"
    compile "uy.kohesive.injekt:injekt-core:1.8.3"
    compile "uy.klutter:klutter-vertx3:0.8.4"
    compile "uy.klutter:klutter-config-typesafe-jdk8:0.8.4"
    compile "io.swagger:swagger-models:1.5.4"
    compile "io.swagger:swagger-parser:1.0.13"
    compile files('lib/vertx-swagger-0.1.0-SNAPSHOT.jar')
    testCompile "org.assertj:assertj-core:3.2.0"
    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:3.1.0"
}

shadowJar {
    classifier = "fat"
    manifest {
        attributes "Main-Class": "org.zalando.axiom.krueger.KruegerApplication"
    }
    mergeServiceFiles {
        include "META-INF/services/io.vertx.core.spi.VerticleFactory"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.8"
}

defaultTasks 'clean', 'test', 'shadowJar'
